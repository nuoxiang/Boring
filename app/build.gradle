apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion

        applicationId "think.boring"

        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

        multiDexKeepProguard file('multidex.pro')
    }

    signingConfigs {
        release {
            try {
                Properties properties = new Properties()
                InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()

                properties.load(inputStream)

                def SIGN_ALIAS = properties.getProperty('SIGN_ALIAS')
                def SIGN_ALIAS_PWD = properties.getProperty('SIGN_ALIAS_PWD')
                def SIGN_PATH = properties.getProperty('SIGN_PATH')
                def SIGN_PWD = properties.getProperty('SIGN_PWD')

                if (SIGN_PATH == null) {
                    return
                }

                keyAlias SIGN_ALIAS
                keyPassword SIGN_ALIAS_PWD
                storeFile file(SIGN_PATH)

                storePassword SIGN_PWD
                v2SigningEnabled true
            }
            catch (ex) {
                throw new Exception("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties")
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
//            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    dexOptions {
        preDexLibraries = false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api project(':base:commonService')

    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
}
